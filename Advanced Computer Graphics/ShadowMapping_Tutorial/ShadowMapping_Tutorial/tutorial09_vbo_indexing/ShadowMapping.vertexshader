#version 330 core

// Input vertex data, different for all executions of this shader.
layout(location = 0) in vec3 vertexPosition_modelspace;
layout(location = 1) in vec2 vertexUV;
layout(location = 2) in vec3 vertexNormal_modelspace;

// Output data ; will be interpolated for each fragment.
out vec2 UV;
out vec2 UV2;
out vec2 UV3;
out vec3 textureMix;
out vec3 Position_worldspace;
out vec3 Normal_cameraspace;
out vec3 EyeDirection_cameraspace;
out vec3 LightDirection_cameraspace;
out vec3 LightDirection_worldspace;
out vec4 lightUV;
out vec3 Normal_worldspace;

// Values that stay constant for the whole mesh.
uniform mat4 MVP;
uniform mat4 V;
uniform mat4 M;
uniform mat4 modelToLightProjectionMatrix;
uniform vec3 LightPosition_worldspace;

void main(){

	// Output position of the vertex, in clip space : MVP * position
	gl_Position =  MVP * vec4(vertexPosition_modelspace,1);
	
	// Position of the vertex, in worldspace : M * position
	Position_worldspace = (M * vec4(vertexPosition_modelspace,1)).xyz;
	
	// Vector that goes from the vertex to the camera, in camera space.
	// In camera space, the camera is at the origin (0,0,0).
	vec3 vertexPosition_cameraspace = ( V * M * vec4(vertexPosition_modelspace,1)).xyz;
	EyeDirection_cameraspace = vec3(0,0,0) - vertexPosition_cameraspace;

	// Vector that goes from the vertex to the light, in camera space. M is ommited because it's identity.
	vec3 LightPosition_cameraspace = ( V * vec4(LightPosition_worldspace,1)).xyz;
	LightDirection_cameraspace = LightPosition_cameraspace + EyeDirection_cameraspace;
	LightDirection_worldspace = LightPosition_worldspace - Position_worldspace;
	
	// Normal of the the vertex, in camera space
	Normal_cameraspace = (M * V *  vec4(vertexNormal_modelspace,0)).xyz; // Only correct if ModelMatrix does not scale the model ! Use its inverse transpose if not.
	
	// Normal of the vertex, in world space
	Normal_worldspace = (M  *  vec4(vertexNormal_modelspace,0)).xyz;
	
	// UV of the vertex. I pass 3 uv set projections
	// one for each axis
	UV = Position_worldspace.yz;
	UV2 = Position_worldspace.xz;
	UV3 = Position_worldspace.xy;
	// the texturemix determines which uv set should be used for which pixel (the blending of the three sets)
	textureMix = abs(Normal_worldspace);
	
	// position of the vertex in light space
	lightUV = modelToLightProjectionMatrix * vec4(vertexPosition_modelspace,1);
	
}

